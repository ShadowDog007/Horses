buildscript {
    repositories {
        jcenter {
            url 'http://jcenter.bintray.com/'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
    }
}

// #################
// #### Plugins ####
// #################
// https://github.com/johnrengelman/shadow
apply plugin: 'shadow'
apply plugin: 'java'
apply plugin: 'maven'   
apply plugin: 'jacoco'


// #######################
// #### Default Tasks ####
// #######################
defaultTasks 'clean', 'build', 'shadowJar', 'test', 'uploadArchives', 'jacocoTestReport'

// #########################
// #### Variable Setup #####
// #########################
group = group_
if (project.hasProperty('teamcity')) {
    
    if (teamcity['RELEASE'] != 'true') {
        version = version_ + '#' + teamcity['build.number']
    } else {
        version = version_
    }
    
    println "##teamcity[buildNumber '${version}']"
} else {
    version = version_ + '-SNAPSHOT'
}

ext {
    localMavenRepoLoc = "file://" + file('../mavenRepo/').absolutePath
}

// #############################
// #### Resourse Filtering #####
// #############################
processResources {
    filter { String line -> line.replace('$(PROJECT_VERSION)', version) }
}

// ###############################
// #### Dependancy shadowing #####
// ###############################
shadow {
    artifactAttached false
    
    // Lombok
    include 'lombok/NonNull.class'
    
    // Relocations
    relocation {
        pattern = 'lombok'
        shadedPattern = 'net.michaelmcknight.horses.dependencies.lombok'
    }
}

// ###################
// #### Archiving ####
// ###################
artifacts {
    archives file('build/distributions/' + name + '-' + version + '.jar')
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: localMavenRepoLoc)
        }
    }
}

// #######################
// #### Code coverage ####
// #######################
jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}
jacocoTestReport.onlyIf { !project.hasProperty('teamcity') }

test {
    jacoco {
        enabled = !project.hasProperty('teamcity')
        includes [ 'build/classes/main/**/*.class' ]
    }
}

// ########################
// #### Gradle wrapper ####
// ########################
task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

// #######################
// #### Dependencies #####
// #######################
repositories {
    mavenCentral()
    // Bukkit repo
    maven { 
        url "http://repo.bukkit.org/content/groups/public/"
    }
    // Vault Repo
    maven {
        url "http://nexus.theyeticave.net/content/repositories/pub_releases/"
    }
    // sk89q repo
    maven {
        url "http://maven.sk89q.com/repo/"
    }
    // md-5 repo
    maven {
        url "http://repo.md-5.net/content/repositories/releases/"
    }
    // spigot repo
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/public/"
    }
}

dependencies {
    compile group: 'org.bukkit', name: 'bukkit', version: '1.9-SNAPSHOT'
    compile group: 'net.milkbowl.vault', name: 'Vault', version: '1.4.2'
    
    compile group: 'com.sk89q', name: 'worldedit', version: '5.6.3'
    compile group: 'com.sk89q', name: 'worldguard', version: '5.9'

    compile 'fr.neatmonster:ncpcore:static'
    
    compile group: 'org.projectlombok', name: 'lombok', version: '1.14.2'
    
    testCompile group: 'junit', name: 'junit', version: '4.+'
}
